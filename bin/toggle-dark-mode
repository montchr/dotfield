#!/usr/bin/env zsh
#
# Toggle system dark mode and run various related effects.
#

emulate -L zsh
setopt extended_glob warn_create_global typeset_silent \
  no_short_loops rc_quotes no_auto_pushd


.kitty-theme-change () {
  ln -sfv \
    "${XDG_CONFIG_HOME}/kitty/base16-kitty/colors/base16-${1}-256.conf" \
    "${XDG_CONFIG_HOME}/kitty/theme.conf"
  kitty @ --to unix:/tmp/kitty-socket \
    set-colors -a -c "${XDG_CONFIG_HOME}/kitty/theme.conf"
}

.emacs-theme-change () {
  emacsclient --no-wait --eval "(+cdom/load-os-theme)"
}

dark-mode && \
  () {
    local appearance=$(cdom_os_appearance)
    local theme=$(cdom_base16_theme ${appearance})
    .kitty-theme-change $theme
    # @TODO set base16-shell and base16-fzf colors on demand
    # base16-set-theme $theme
    .emacs-theme-change $theme
  }

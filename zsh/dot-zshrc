# -*- mode: sh; eval: (sh-set-shell "zsh") -*-

# - - - - - - - - - - - - - - - - - - - -
# Instant Prompt
# - - - - - - - - - - - - - - - - - - - -

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# - - - - - - - - - - - - - - - - - - - -
# Zsh Core Configuration
# - - - - - - - - - - - - - - - - - - - -

typeset -g \
  HISTSIZE=290000 \
  SAVEHIST=290000 \
  HISTFILE=~/.zhistory \

[[ -d "$CACHEDIR" ]] || mkdir -p "$CACHEDIR"
[[ -d "$XDG_RUNTIME_DIR" ]] || mkdir -p "$XDG_RUNTIME_DIR"

autoload -Uz add-zsh-hook

# Use chpwd_recent_dirs to start new sessions from last working dir
# Populate dirstack with chpwd history
autoload -Uz chpwd_recent_dirs
add-zsh-hook chpwd chpwd_recent_dirs
zstyle ':chpwd:*' recent-dirs-file "${TMPDIR}/chpwd-recent-dirs"
dirstack=($(awk -F"'" '{print $2}' ${$(zstyle -L ':chpwd:*' recent-dirs-file)[4]} 2>/dev/null))
[[ ${PWD} = ${HOME}  || ${PWD} = "." ]] && (){
  local dir
  for dir ($dirstack){
    [[ -d ${dir} ]] && { cd -q ${dir}; break }
  }
} 2>/dev/null


ZINIT_HOME="${ZINIT_HOME:-${ZPLG_HOME:-${ZDOTDIR:-${HOME}}/.zinit}}"
ZINIT_BIN_DIR_NAME="${${ZINIT_BIN_DIR_NAME:-${ZPLG_BIN_DIR_NAME}}:-bin}"
### Added by Zinit's installer
if [[ ! -f ${ZINIT_HOME}/${ZINIT_BIN_DIR_NAME}/zinit.zsh ]]; then
  print -P "%F{33}▓▒░ %F{220}Installing DHARMA Initiative Plugin Manager (zdharma/zinit)…%f"
  command mkdir -p "${ZINIT_HOME}" && command chmod g-rwX "${ZINIT_HOME}"
  command git clone https://github.com/zdharma/zinit "${ZINIT_HOME}/${ZINIT_BIN_DIR_NAME}" && \
    print -P "%F{33}▓▒░ %F{34}Installation successful.%f" || \
    print -P "%F{160}▓▒░ The clone has failed.%f"
fi
source "${ZINIT_HOME}/${ZINIT_BIN_DIR_NAME}/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
  zinit-zsh/z-a-rust \
  zinit-zsh/z-a-as-monitor \
  zinit-zsh/z-a-patch-dl \
  zinit-zsh/z-a-bin-gem-node

### End of Zinit's installer chunk


# zt() : First argument is a wait time and suffix, ie "0a". Anything that
# doesn't match will be passed as if it were an ice mod. Default ices depth'3'
# and lucid
zt()  { zinit depth'3' lucid ${1/#[0-9][a-c]/wait"$1"} "${@:2}"; }

# - - - - - - - - - - - - - - - - - - - -
# Initial Prompt
# Annexes
# Config Source
# - - - - - - - - - - - - - - - - - - - -

zt light-mode for \
  pick'async.zsh' \
    mafredri/zsh-async \

zinit ice depth=1
zinit light romkatv/powerlevel10k

# Load config files plugin.
zt light-mode for \
  _local/config-files

#
# Plugins
# - - - - - - - - - - - - - - - - - - - -

    # atinit"
    #     ZSH_TMUX_FIXTERM=true
    #     ZSH_TMUX_AUTOSTART=true
    #     ZSH_TMUX_AUTOCONNECT=true
    # " \
    #     OMZP::tmux \

# Explanation:
# - Loading tmux first, to prevent jumps when tmux is loaded after .zshrc
# - History plugin is loaded early (as it has some defaults) to prevent empty history stack for other plugins
zt for \
    atinit"HIST_STAMPS=yyyy.mm.dd" \
        OMZL::history.zsh

zt 0a for \
  trigger-load"!base16-set-shell" \
  pick"scripts/base16-${BASE16_THEME}.sh" \
  nocompile \
    chriskempson/base16-shell

zt 0a for \
    atload"
        alias ..='cd ..'
        alias ...='cd ../..'
        alias ....='cd ../../..'
        alias .....='cd ../../../..'
        alias ......='cd ../../../../..'
        alias l='exa --oneline'
        alias ll='exa -Flagh --git'
        alias la='exa -Fal'
        alias ls='exa --group-directories-first'
        alias lld='exa -Flagh --git --group-directories-first'
        alias ld='exa -D1'
        alias tree='exa --tree'
    " \
        OMZL::directories.zsh \
    OMZL::git.zsh \
    OMZL::grep.zsh \
    OMZL::termsupport.zsh \
    atload"
        alias gs='git status'
        alias gap='git add -p'
        alias gdt='git difftool'
        alias gca='git commit --amend'
        alias gcam='git commit --amend -C HEAD'
        alias grs='git reset'
        alias gcob='git checkout -b'
        alias gpl='git pull'
        alias gplo='git pull origin'
        alias gpls='git pull && gsumo'
        alias gps='git push'
        alias gpsu='git push -u'
        alias gpso='git push origin'
        alias gpss='git push && gsumo'
        alias gsh='git stash'
        alias gsha='git stash apply'
        alias gshp='git stash pop'
        alias gshl='git stash list'
        alias gl='git log --oneline --decorate -20'
    " \
        OMZP::git \
    OMZ::plugins/extract/extract.plugin.zsh \
    skywind3000/z.lua \
    djui/alias-tips

zt 0a for \
  trigger-load'!base16-fzf' \
  pick"bash/base16-${BASE16_THEME}.config" \
  nocompile'!' \
    nicodebo/base16-fzf \
  atload'_zsh_autosuggest_start' \
    zsh-users/zsh-autosuggestions \
  if'false' ver'dev' \
    marlonrichert/zsh-autocomplete

zt 0b light-mode for \
    atload'export ENHANCD_FILTER=fzf:fzy:peco' \
        b4b4r07/enhancd \
    atload'
        bindkey "$terminfo[kcuu1]" history-substring-search-up;
        bindkey "$terminfo[kcud1]" history-substring-search-down;
        bindkey "^[[A" history-substring-search-up;
        bindkey "^[[B" history-substring-search-down;
        bindkey -M vicmd "k" history-substring-search-up;
        bindkey -M vicmd "j" history-substring-search-down;
    ' \
        zsh-users/zsh-history-substring-search \
    bindmap"^R -> ^H" atinit"
        zstyle :history-search-multi-word page-size 10
        zstyle :history-search-multi-word highlight-color fg=red,bold
        zstyle :plugin:history-search-multi-word reset-prompt-protect 1
    " \
      zdharma/history-search-multi-word \
    autoload'#manydots-magic' \
        knu/zsh-manydots-magic \
    pick'autopair.zsh' nocompletions atload'ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(autopair-insert)' \
        hlissner/zsh-autopair \

zt 0b light-mode from'gh-r' as'program' for \
    junegunn/fzf \
    sei40kr/fast-alias-tips-bin \
    sei40kr/zsh-fast-alias-tips


# Load additional fzf helpers
zt 0c light-mode for \
    as"command" pick"bin/fzf-tmux" \
        junegunn/fzf \
    multisrc"shell/{completion,key-bindings}.zsh" \
    id-as"junegunn/fzf_completions" \
    pick"/dev/null" \
        junegunn/fzf

zt 0c light-mode atinit"
    typeset -gA FAST_HIGHLIGHT;
    FAST_HIGHLIGHT[git-cmsg-len]=100;
    ZINIT[COMPINIT_OPTS]=-C;
    zicompinit;
    zicdreplay;
  " for zdharma/fast-syntax-highlighting


zt 0c light-mode binary for \
  sbin'bin/git-ignore' atload'export GI_TEMPLATE="$PWD/.git-ignore"; alias gi="git-ignore"' \
    laggardkernel/git-ignore

zt 0c light-mode null for \
  sbin \
    paulirish/git-open \
  sbin'*/rm-trash' reset \
  patch"$pchf/%PLUGIN%.patch" \
    nateshmbhat/rm-trash

zt 1a for \
  atload"
    # Use fzf-tab after second tab press
    # via https://github.com/mrksr/dotfiles/blob/e3dd8bde7d7be6a294f5a2a245cb7e4a15837d71/shell/.zshrc#L101-L113
    fzf-tab-partial-and-complete() {
      if [[ \$LASTWIDGET = 'fzf-tab-partial-and-complete' ]]; then
        fzf-tab-complete
      else
        zle complete-word
      fi
    }

    zle -N fzf-tab-partial-and-complete
    bindkey '^I' fzf-tab-partial-and-complete
  " \
    Aloxaf/fzf-tab \
  zdharma/zui \
  zdharma/zbrowse

zt 2a light-mode for \
  OMZP::command-not-found \
  OMZP::composer \
  ael-code/zsh-colored-man-pages \
  wfxr/forgit

zt 2b light-mode for \
  as"command" \
  pick"colortest" \
  atload"alias colortest=\"colortest base16-${BASE16_THEME}.sh\"" \
  blockf \
    chriskempson/base16-shell


# - - - - - - - - - - - - - - - - - - - -
# - asdf version manager
# - direnv via asdf
# - - - - - - - - - - - - - - - - - - - -

export ASDF_DIRENV_VERSION="2.27.0"

function .asdf-plugins {
  local p
  for p in $cdom_asdf_plugins; do asdf plugin ${1} $p; done
}

function .load-asdf-direnv {
  zt 0a for \
    id-as'asdf-init' \
    atclone"
      .asdf-plugins add
      asdf install
      asdf install direnv ${ASDF_DIRENV_VERSION}
    " \
    atpull'asdf update; asdf plugin update --all; .asdf-plugins reshim' \
      @zdharma/null
  zt 0b for \
    id-as'asdf-direnv-init' \
    atclone"
      env ASDF_DIRENV_VERSION=${ASDF_DIRENV_VERSION}
      asdf exec direnv hook zsh > zhook.zsh
    " \
    atpull'%atclone' \
    atinit'direnv() { env ASDF_DIRENV_VERSION=${ASDF_DIRENV_VERSION} asdf exec direnv ${@}; }' \
    src'zhook.zsh' \
      @zdharma/null
}

# Initialize ASDF-VM
# @TODO prevent output?
zt 0a for \
  atclone'[[ ! -d "$ASDF_DATA_DIR" ]] && mkdir -p "$ASDF_DATA_DIR"' \
  atload'.load-asdf-direnv' \
  pick'asdf.sh' \
    @asdf-vm/asdf

# Update plugins upon changing the global tool versions file.
zinit subscribe'~/.tool-versions' for \
  atload'
    asdf update; asdf plugin update --all; asdf install
    .asdf-plugins reshim
  ' \
    @zdharma/null


# - - - - - - - - - - - - - - - - - - - -
# - Completions
# - Cleanup
# - - - - - - - - - - - - - - - - - - - -

zt 2b light-mode for \
  trigger-load'!gencomp' pick'zsh-completion-generator.plugin.zsh' blockf \
  atload'alias gencomp="zinit silent nocd as\"null\" wait\"2\" atload\"zinit creinstall -q _local/config-files; zicompinit\" for /dev/null; gencomp"' \
    RobSis/zsh-completion-generator

zt 2c blockf for \
  atpull'zinit creinstall -q .' \
  atload"zicompinit; zicdreplay" \
    zsh-users/zsh-completions

zt 2c for \
  id-as'Cleanup' nocd \
  atinit'unset -f zt; _zsh_autosuggest_bind_widgets' \
    zdharma/null


# - - - - - - - - - - - - - - - - - - - -
# Miscellaneous
# - - - - - - - - - - - - - - - - - - - -

zinit is-snippet for \
    if"[[ -f $HOME/.localrc ]]" $HOME/.localrc

# To customize prompt, run `p10k configure` or edit ~/.dots/zsh/dot-p10k.zsh.
[[ ! -f ~/.dots/zsh/dot-p10k.zsh ]] || source ~/.dots/zsh/dot-p10k.zsh
